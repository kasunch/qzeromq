cmake_minimum_required(VERSION 3.1)

project (qzmq CXX)
set(CMAKE_CXX_STANDARD 11)

if(NOT SET_UP_CONFIGURATIONS_DONE)
    set(SET_UP_CONFIGURATIONS_DONE TRUE)
    # No reason to set CMAKE_CONFIGURATION_TYPES if it's not a multiconfig generator
    # Also no reason mess with CMAKE_BUILD_TYPE if it's a multiconfig generator.
    get_property(isMultiConfig GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG)
    if(isMultiConfig)
        set(CMAKE_CONFIGURATION_TYPES "Debug;Release" CACHE STRING "" FORCE) 
    else()
        if(NOT CMAKE_BUILD_TYPE)
            message(STATUS "Defaulting to release build.")
            set(CMAKE_BUILD_TYPE Release CACHE STRING "" FORCE)
        endif()
        set_property(CACHE CMAKE_BUILD_TYPE PROPERTY HELPSTRING "Choose the type of build")
        # set the valid options for cmake-gui drop-down list
        set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS "Debug;Release;Profile")
    endif()
endif()

option(USE_CONAN_BUILD_INFO "Whether or not to use conanbuildinfo.cmake file"  OFF)
option(BUILD_SHARED "Whether or not to build the shared object"  ON)
option(BUILD_STATIC "Whether or not to build the static archive" ON)
option(ZMQ_SHARED "Whether or not to use ZeroMQ shared library" OFF)
option(WITH_PERF_TOOL "Whether or not to perf tools" OFF)

if (USE_CONAN_BUILD_INFO)
    include(${CMAKE_BINARY_DIR}/conanbuildinfo.cmake)
    conan_basic_setup()
    if(ZMQ_SHARED)
        set(libzmq_shared ZeroMQ::libzmq)
    else()
        set(libzmq_static ZeroMQ::libzmq-static)
    endif()
else()
    if(ZMQ_SHARED)
        set(libzmq_shared libzmq)
    else()
        set(libzmq_static libzmq-static)
    endif()
endif()

find_package(Qt5 COMPONENTS Core REQUIRED)
find_package(ZeroMQ REQUIRED)
find_package(Threads REQUIRED)

set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

add_subdirectory(src)
if (WITH_PERF_TOOL)
    add_subdirectory(perf)
endif()